group 'nian.so'
version '1.0-snapshot'

apply plugin: 'java'
apply plugin: 'application'
apply from: "$rootProject.projectDir/dependencies.gradle"
apply from: "$rootProject.projectDir/utils.gradle"

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    mavenCentral()
}

dependencies {
    compile(
            libraries."spring-context",
            libraries."slf4j-api",
            libraries."logback-core",
            libraries."logback-access",
            libraries."logback-classic",
            libraries."logback-ext-spring",
            libraries."jackson-databind",
            libraries."freemarker",
            libraries."httpclient",
            libraries."httpmime",
            libraries."joda-time",
            libraries."commons-codec",
            libraries."common-lang3"
    )

    testCompile libraries."junit"
}

archivesBaseName = "nian-backup"
mainClassName = "so.nian.backup.export.NianExport"

/*编码UTF-8*/
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

tasks.withType(JavaExec) {
    jvmArgs += ['-Dfile.encoding=UTF-8']
}


/*打包resources目录*/
applicationDistribution.from(sourceSets.main.resources.srcDirs) {
    into ""
}
/*启动脚本：CLASSPATH中增加APP_HOME*/
startScripts {
    doLast {
        unixScript.text = unixScript.text.replace(
                'CLASSPATH=\$APP_HOME/', 'CLASSPATH=\$APP_HOME:\$APP_HOME/')
        windowsScript.text = windowsScript.text.replace(
                'CLASSPATH=%APP_HOME%\\', 'CLASSPATH=%APP_HOME%;%APP_HOME%\\')
    }
}

jar {
    excludes = ['config/']
    doFirst {
        /* 生成 manifest 文件 */
        manifest = defaultManifest([
                project      : project,
                projectVendor: "http://nian.so/#!user/142171"
        ])
    }
}
